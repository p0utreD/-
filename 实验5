;**************************************
; 定时实验：利用定时器T0产生1s的定时程序
; 在P1.7端口产生周期为2秒的方波，控制L7亮1秒、暗1秒
; 晶振频率：11.0592MHz
;**************************************

        ORG     0000H
        LJMP    MAIN            ;上电，转向主程序
        
        ORG     000BH           ;T0的中断入口地址
        LJMP    SERVE           ;转向中断服务程序
        
;**************************************
; 主程序
;**************************************
        ORG     0030H           ;主程序
MAIN:   MOV     SP,#60H         ;设堆栈指针
        MOV     R7,#0           ;计数器初始化为0
        MOV     TMOD,#01H       ;设置T0工作于模式1
        
        ; 计算定时器初值
        ; 11.0592MHz晶振，机器周期=12/11.0592=1.085us
        ; 50ms需要计数：50000/1.085≈46083次
        ; 65536-46083=19453 (4BFDH)
        MOV     TL0,#0FDH       ;装入计数值低8位
        MOV     TH0,#4BH        ;装入计数值高8位
        
        SETB    TR0             ;启动定时器T0
        SETB    ET0             ;允许T0中断
        SETB    EA              ;允许CPU中断
        
        SETB    P1.7            ;初始化P1.7为高电平(LED灭)

LOOP:   SJMP    LOOP            ;等待中断

;**************************************
; 中断服务程序
;**************************************
SERVE:  MOV     TL0,#0FDH       ;重装初值
        MOV     TH0,#4BH
        
        INC     R7              ;计数器+1
        MOV     A,R7
        CJNE    A,#20,EXIT_INT  ;判断是否达到20次(20*50ms=1s)
        
        MOV     R7,#0           ;计数器清零
        CPL     P1.7            ;P1.7取反，LED状态翻转
        
EXIT_INT:
        RETI                    ;中断返回
        
        END
